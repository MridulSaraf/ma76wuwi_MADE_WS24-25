pipeline BaumPipeline {

    // Extractor Block: Extracts trees CSV file 
    block DataExtractor oftype HttpExtractor {
        url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv";
    }

    // Transformer Block: Converts trees File to TextFile (In: File, Out: TextFile)
    block TreesTextFileInterpreter oftype TextFileInterpreter {
        encoding: "latin6";
    }

    // Transformer Block: Converts trees TextFile to Sheet (In: TextFile, Out: Sheet)
    block CSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    // Drop "baumart_deutsch" column 
    block ColumnDeleter oftype ColumnDeleter {
        delete: [column E];
    }

    // Transformer Block: Converts trees Sheet to Table (In: Sheet, Out: Table)
    block TableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "lfd_nr" oftype BIGINT,
            "stadtteil" oftype Vogelsang_defined,
            "standort" oftype TEXT,
            "baumart_botanisch" oftype TEXT,
            "id" oftype GeoPoint_defined,
            "baumfamilie" oftype TEXT
        ];
    }

    block DataLoader oftype SQLiteLoader {
        table: "trees";
        file: "./trees.sqlite";
        dropTable: true;  // Drops table to remove previous data and ensure clean load
    }

    // Constraint: Only valid "stadtteil" starting with "Vogelsang"
    constraint Vogelsang_defined_format oftype RegexConstraint {
        regex: /^Vogelsang-/;
    }

    valuetype Vogelsang_defined oftype TEXT { constraints: [Vogelsang_defined_format]; }

    // Constraint: Valid "id" contains geo-coordinates with pattern {geo-coordinate 1}, {geo-coordinate 2}
    constraint GeoPoint_defined_format oftype RegexConstraint {
        regex: /^(\d{1,3}\.\d+),\s*(\d{1,3}\.\d+)$/;
    }

    valuetype GeoPoint_defined oftype TEXT { constraints: [GeoPoint_defined_format]; }

    // Pipe: Build a chain of all the BaumPipeline's blocks
    DataExtractor
    -> TreesTextFileInterpreter
    -> CSVInterpreter
    -> ColumnDeleter
    -> TableInterpreter
    -> DataLoader;
}
